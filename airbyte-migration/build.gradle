import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

plugins {
    id 'application'
    id 'com.bmuschko.docker-remote-api'
}

dependencies {
    implementation project(':airbyte-json-validation')

    implementation 'net.sourceforge.argparse4j:argparse4j:0.8.1'

    testImplementation project(':airbyte-config:models')
    testImplementation project(':airbyte-db:lib')
}

application {
    mainClass = 'io.airbyte.migrate.MigrationRunner'
}

task copyDocker(type: Copy) {
    delete "build/docker"

    from "${project.projectDir}/Dockerfile"
    into "build/docker/"
}

task copyGeneratedTar(type: Copy) {
    dependsOn distTar
    dependsOn copyDocker

    from('build/distributions') {
        include 'airbyte-migration-*.tar'
    }
    into 'build/docker/bin'
}

// Use task types
task buildDockerImage(type: DockerBuildImage) {
    dependsOn copyDocker
    dependsOn copyGeneratedTar
    inputDir = file('build/docker')
    images.add("airbyte/migration:$version")
}
build.dependsOn(buildDockerImage)
